class Post
/*
	Defines the details of the post using types from Utils class and contains operations to work with posts.
*/

instance variables
	private title : Utils`String;
	private content : Utils`String;
	private author : RegisteredUser;
	private date : Date;
	private views : nat := 0;
	private rating : int := 0;
	
	private tagsList : set of Utils`String;
	private commentsList : set of Comment := {};
	
operations
	public Post: Utils`String * Utils`String * RegisteredUser * set of Utils`String * nat1 * nat1 * nat1 ==> Post
		Post(postTitle, postContent, postAuthor, postTags, day, month, year) == 
		(	title := postTitle;
			content := postContent;
			author := postAuthor;
			tagsList := postTags;
			date := new Date(year, month, day);
			return self;
		)
		post title = postTitle and content = postContent and author = postAuthor and tagsList = postTags 
				and views = 0 and rating = 0 and commentsList = {} and date.year = year and date.month = month and date.day = day;
		
	public editPost: Utils`String * Utils`String * set of Utils`String ==> Post
		editPost(newTitle, newContent, newTags) == 
		(	title := newTitle;
			content := newContent;
			tagsList := newTags;
			return self;
		)
		post content = newContent and title = newTitle and tagsList = newTags;
	
  public addComment: Comment ==> ()
  	addComment(comment) == 
  	(	commentsList := commentsList union {comment};
		);
	
  public removeComment: Comment ==> ()
  	removeComment(comment) == 
  	(	commentsList := commentsList \ {comment};
		)
		pre comment in set commentsList;
		
  public increaseRating: () ==> ()
  	increaseRating() == 
  	(	rating := rating + 1;
			author.addReputation(1)
		);
		
  public decreaseRating: () ==> ()
  	decreaseRating() == 
  	(	rating := rating - 1;
			author.addReputation(-1)
		);
	        
	/* Get Operations */        
  public getTitle: () ==> Utils`String
  	getTitle() == 
  	(	return title;
  	)
    pre title <> []
    post RESULT = title;
    
  public getContent: () ==> Utils`String
  	getContent() == 
  	(	return content;
  	)
    pre content <> []
    post RESULT = content;
    
  public getAuthor: () ==> RegisteredUser
  	getAuthor() == 
  	(	return author;
  	)
    post RESULT = author;
    
  public getDate: () ==> Date
  	getDate() == 
  	( return date;
  	)
    post RESULT = date;
    
  public getRating: () ==> int
  	getRating() == 
  	( return rating;
  	)
    post RESULT = rating;
    
  public getViews: () ==> nat
  	getViews() == 
  	( return views;
  	)
  	pre (views >= 0)
    post RESULT = views;
    
  public getTagsList: () ==> set of Utils`String
  	getTagsList() == 
  	( return tagsList;
  	)
    pre tagsList <> {}
    post RESULT = tagsList;
    
  public getCommentsList: () ==> set of Comment
  	getCommentsList() == 
  	( return commentsList;
  	)
    post RESULT = commentsList;

end Post