class Comment
types
	public String = seq of char;
	
	public Date :: year : nat1
								 month: nat1
								 day : nat1
								 inv mk_Date(y,m,d) == m <= 12 and d <= Utils`DaysOfMonth(m);

instance variables
	private content : String;
	private author : RegisteredUser;
	private date : Date;
	private rating : int := 0;

operations	
	public Comment: String * RegisteredUser * nat1 * nat1 * nat1 ==> Comment
		Comment(commentContent, commentAuthor, day, month, year) == (
			content := commentContent;
			author := commentAuthor;
			date := mk_Date(year, month, day);
		)
		post content = commentContent and author = commentAuthor and rating = 0 and date.year = year and date.month = month and date.day = day;	
	
	public editComment: String ==> Comment
		editComment(newContent) == (
			content := newContent;
			return self;
		)
		post content = newContent;		
	
  public increaseRating: () ==> ()
  	increaseRating() == (
  		rating := rating + 1;
			author.addReputation(3)
		);
		
  public decreaseRating: () ==> ()
  	decreaseRating() == (
  		rating := rating - 1;
			author.addReputation(-2)
		);
	
	public getContent: () ==> String
  	getContent() == return content
    pre content <> []
    post RESULT = content;
    
  public getAuthor: () ==> RegisteredUser
  	getAuthor() == return author
    post RESULT = author;
    
  public getDate: () ==> Date
  	getDate() == return date
    post RESULT = date;
    
  public getRating: () ==> int
  	getRating() == return rating
    post RESULT = rating;
	
end Comment